set(CMAKE_CROSSCOMPILING TRUE)
set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_SYSTEM_PROCESSOR riscv)

set(TARGET riscv${RISCV_XLEN}-unknown-elf)
list(JOIN RISCV_EXTENSIONS "" RISCV_EXTENSIONS_JOINED)
set(MARCH rv${RISCV_XLEN}i${RISCV_EXTENSIONS_JOINED})

list(FIND RISCV_EXTENSIONS d HAVE_RISCV_DOUBLE)
list(FIND RISCV_EXTENSIONS f HAVE_RISCV_FLOAT)

if("${HAVE_RISCV_DOUBLE}" GREATER "-1")
set(RISCV_ABI_FLOAT d)
elseif("${HAVE_RISCV_FLOAT}" GREATER "-1")
set(RISCV_ABI_FLOAT f)
else()
unset(RISCV_ABI_FLOAT)
endif()

if("${RISCV_XLEN}" EQUAL "32")
set(MABI ilp${RISCV_XLEN}${RISCV_ABI_FLOAT})
else()
set(MABI lp${RISCV_XLEN}${RISCV_ABI_FLOAT})
endif()
set(MCMODEL medany)

set(RISCV_EXTENSIONS_MATTR_LIST ${RISCV_EXTENSIONS})
list(TRANSFORM RISCV_EXTENSIONS_MATTR_LIST REPLACE "^(.+)$" ",-mllvm=--mattr=+\\1")
list(JOIN RISCV_EXTENSIONS_MATTR_LIST "" RISCV_EXTENSIONS_WL_MATTR)

cmake_path(SET RISCV_TOOLCHAIN_DIR NORMALIZE ${CMAKE_CURRENT_LIST_DIR})
set(LLVM_PREFIX ${RISCV_TOOLCHAIN_DIR}/bin/)
set(LLVM_SUFFIX "")
set(RISCV_COMMON_FLAGS "-march=${MARCH} -mabi=${MABI} -mcmodel=${MCMODEL} -mno-relax")

set(CMAKE_C_COMPILER_TARGET ${TARGET})
set(CMAKE_CXX_COMPILER_TARGET ${TARGET})
set(CMAKE_ASM_COMPILER_TARGET ${TARGET})
set(CMAKE_C_FLAGS_INIT	"${CMAKE_C_FLAGS} ${RISCV_COMMON_FLAGS}")
set(CMAKE_CXX_FLAGS_INIT	"${CMAKE_CXX_FLAGS} ${RISCV_COMMON_FLAGS}")
set(CMAKE_ASM_FLAGS_INIT	"${CMAKE_ASM_FLAGS} ${RISCV_COMMON_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS_INIT	"${CMAKE_EXE_LINKER_FLAGS} -fuse-ld=lld -Wl,--verbose -v -Wl,-v -nostdlib -nostartfiles -static -Wl,-mllvm=--target-abi=${MABI} -Wl${RISCV_EXTENSIONS_WL_MATTR}")#-mllvm=--mattr=+m,-mllvm=--mattr=+a,-mllvm=--mattr=+f,-mllvm=--mattr=+d,-mllvm=--mattr=+c")

set(CMAKE_C_COMPILER	${LLVM_PREFIX}clang${LLVM_SUFFIX})
set(CMAKE_CXX_COMPILER	${LLVM_PREFIX}clang++${LLVM_SUFFIX})
set(CMAKE_ASM_COMPILER	${LLVM_PREFIX}clang${LLVM_SUFFIX})
set(CMAKE_ADDR2LINE	${LLVM_PREFIX}llvm-addr2line${LLVM_SUFFIX})
set(CMAKE_AR		${LLVM_PREFIX}llvm-ar${LLVM_SUFFIX})
set(CMAKE_NM		${LLVM_PREFIX}llvm-nm${LLVM_SUFFIX})
set(CMAKE_RANLIB	${LLVM_PREFIX}llvm-ranlib${LLVM_SUFFIX})
set(CMAKE_READELF	${LLVM_PREFIX}llvm-readelf${LLVM_SUFFIX})
set(CMAKE_OBJCOPY	${LLVM_PREFIX}llvm-objcopy${LLVM_SUFFIX})
set(CMAKE_OBJDUMP	${LLVM_PREFIX}llvm-objdump${LLVM_SUFFIX})
set(CMAKE_STRIP		${LLVM_PREFIX}llvm-strip${LLVM_SUFFIX})
set(CMAKE_SIZE      ${LLVM_PREFIX}llvm-size${LLVM_SUFFIX})
set(CMAKE_LINKER	${LLVM_PREFIX}lld${LLVM_SUFFIX})

